# Author: Kien Tran
# Date: 11/16/2020

# Volume Weight MACD

declare lower;

input fastLength = 12;
input slowLength = 26;
input MACDLength = 9;

def fastAvg = sum(volume * close, fastLength) / sum(volume, fastLength);
def slowAvg = sum(volume * close, slowLength) / sum(volume, slowLength);
def Value = fastAvg - slowAvg;
def Avg = ExpAverage(Value, MACDLength);
# Diff defines the bar color that we will be using as our first indicator (1/3)
def Diff = Value - avg;
def volumeWeightMACDColor;
if (Diff >= 0){
    volumeWeightMACDColor = 1;
} else {
    volumeWeightMACDColor = -1;
}


# Stochastic Full

input KPeriod = 10;
input priceH = high;
input priceL = low;
input priceC = close;
input slowing_period = 3;
input averageStochasticFullType = AverageType.SIMPLE;

def lowest_k = Lowest(priceL, KPeriod);
def c1 = priceC - lowest_k;
def c2 = Highest(priceH, KPeriod) - lowest_k;
def FastK = if c2 != 0 then c1 / c2 * 100 else 0;

# FullK is our second indicator (2/3)
def FullK = MovingAverage(averageStochasticFullType, FastK, slowing_period);

# Relative Strength Index

input length = 14;
input price = close;
input averageType = AverageType.WILDERS;

def NetChgAvg = MovingAverage(averageType, price - price[1], length);
def TotChgAvg = MovingAverage(averageType, AbsValue(price - price[1]), length);
def ChgRatio = if TotChgAvg != 0 then NetChgAvg / TotChgAvg else 0;

# RSI is the last indicator we will be looking at to trigger our signal (3/3)
def RSI = 50 * (ChgRatio + 1);


plot Signal;
# Buy/Sell Signal Calculations
if (volumeWeightMACDColor == -1) then {
    if (FullK[1] < 20 && FullK >= 20) then {
        if (RSI <= 30) then {
            Signal = 2; # Super strong buy
        } else {
            Signal = 1; # Strong buy
        }
    } else if (FullK <= 40) then {
        Signal = .5; # Weak buy
    } else {
        Signal = 0;
    }
    
} else  {
    if (FullK[1] > 80 && FullK <= 80) then {
        if (RSI >= 70) then {
            Signal = -2; # Super strong sell
        } else {
            Signal = -1; # Strong sell
        }
    } else if (FullK >= 60) then {
        Signal = -.5; # Weak sell
    } else {
        Signal = 0;
    }
    
}

Signal.SetPaintingStrategy(PaintingStrategy.HISTOGRAM);
Signal.SetLineWeight(3);
Signal.DefineColor("Super Strong Buy", Color.DARK_GREEN);
Signal.DefineColor("Strong Buy", Color.GREEN);
Signal.DefineColor("Weak Buy", Color.LIGHT_GREEN);

Signal.DefineColor("Super Strong Sell", Color.DARK_RED);
Signal.DefineColor("Strong Sell", Color.RED);
Signal.DefineColor("Weak Sell", Color.LIGHT_RED);

Signal.DefineColor("No Signal", Color.Gray);


Signal.AssignValueColor(if signal > 0 then if signal == 2 then Signal.color("Super Strong Buy") else if signal == 1 then Signal.color("Strong Buy") else Signal.color("Weak Buy") else if signal < 0 then if signal == -2 then Signal.color("Super Strong Sell") else if signal == -1 then Signal.color("Strong Sell") else Signal.color("Weak Sell") else Signal.color("No Signal"));

# Plots the zero line base of user choice of color
plot ZeroLine = 0;
ZeroLine.SetDefaultColor(GetColor(0));