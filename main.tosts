# Volume Weight MACD

declare lower;

input fastLength = 12;
input slowLength = 26;
input MACDLength = 9;

def fastAvg = sum(volume * close, fastLength) / sum(volume, fastLength);
def slowAvg = sum(volume * close, slowLength) / sum(volume, slowLength);
def Value = fastAvg - slowAvg;
def Avg = ExpAverage(Value, MACDLength);
# Diff defines the bar color that we will be using as our first indicator (1/3)
def Diff = Value - avg;
def volumeWeightMACDColor;
if (Diff >= 0){
    volumeWeightMACDColor = 1;
} else {
    volumeWeightMACDColor = -1;
}

# Diff.SetPaintingStrategy(PaintingStrategy.HISTOGRAM);
# Diff.SetLineWeight(3);
# Diff.DefineColor("Positive and Up", Color.GREEN);
# Diff.DefineColor("Positive and Down", Color.DARK_GREEN);
# Diff.DefineColor("Negative and Down", Color.RED);
# Diff.DefineColor("Negative and Up", Color.DARK_RED);
# Diff.AssignValueColor(if diff >= 0 then if diff > diff[1] then Diff.color("Positive and Up") else Diff.color("Positive and Down") else if diff < diff[1] then Diff.color("Negative and Down") else Diff.color("Negative and Up"));


# Stochastic Full

input KPeriod = 10;
input priceH = high;
input priceL = low;
input priceC = close;
input slowing_period = 3;
input averageStochasticFullType = AverageType.SIMPLE;

def lowest_k = Lowest(priceL, KPeriod);
def c1 = priceC - lowest_k;
def c2 = Highest(priceH, KPeriod) - lowest_k;
def FastK = if c2 != 0 then c1 / c2 * 100 else 0;

# FullK is our second indicator (2/3)
def FullK = MovingAverage(averageStochasticFullType, FastK, slowing_period);

# Relative Strength Index

input length = 14;
input price = close;
input averageType = AverageType.WILDERS;

def NetChgAvg = MovingAverage(averageType, price - price[1], length);
def TotChgAvg = MovingAverage(averageType, AbsValue(price - price[1]), length);
def ChgRatio = if TotChgAvg != 0 then NetChgAvg / TotChgAvg else 0;

# RSI is the last indicator we will be looking at to trigger our signal (3/3)
def RSI = 50 * (ChgRatio + 1);


plot signal;
# Buy/Sell Signal Calculations
if (volumeWeightMACDColor == -1) then {
    if (FullK <= 20) then {
        if (RSI <= 30) then {
            signal = 2; # Super strong buy
        } else {
            signal = 1; # Strong buy
        }
    } else if (FullK <= 40) then {
        signal = .5; # Weak buy
    } else {
        signal = 0;
    }
    
} else  {
    if (FullK >= 80) then {
        if (RSI >= 70) then {
            signal = -2; # Super strong sell
        } else {
            signal = -1; # Strong sell
        }
    } else if (FullK >= 60) then {
        signal = -.5; # Weak sell
    } else {
        signal = 0;
    }
    
}


# Plots the zero line base of user choice of color
plot ZeroLine = 0;
ZeroLine.SetDefaultColor(GetColor(0));